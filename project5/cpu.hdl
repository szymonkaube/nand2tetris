/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
      // decide whether A instruction
      Not(in=instruction[15], out=isAInstruct);
    
      // choose instruction if A instruction
      Mux16(a=aluOut, b=instruction, sel=isAInstruct, out=aMux);
      
      // if A instruction or destination is A then save
      And(a=isAInstruct, b=instruction[5], out=isCInstructToA);
      
      Or(a=isAInstruct, b=isCInstructToA, out=aRegLoad);
      Register(in=aMux, load=aRegLoad, out=aReg, out[0..14]=addressM);

      // if d2 and C instruction then save ALU output into D reg
      And(a=instruction[15], b=instruction[4], out=saveToD);
      Register(in=aluOut, load=saveToD, out=dReg);

      // if a bit then we want to do something with inM
      Mux16(a=aReg, b=inM, sel=instruction[12], out=aluInputB);

      // computation using ALU
      ALU(x=dReg, y=aluInputB, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=aluOut, zr=outZr, ng=outNg);

      // if C instruction then pass d3 as writeM
      And(a=instruction[15], b=instruction[3], out=writeM);

      // if j1 bit and ALU ng bit
      And(a=instruction[2], b=outNg, out=pcNgCond);
      // if j2 bit and ALU zr bit
      And(a=instruction[1], b=outZr, out=pcZrCond);
      // if j3 bit and not negative and not zero
      Not(in=outNg, out=outNonNeg);
      Not(in=outZr, out=outNonZero);
      And(a=outNonNeg, b=outNonZero, out=outPos);
      And(a=outPos, b=instruction[0], out=pcPosCond);

      // one of three jump conditions is true...
      Or(a=pcNgCond, b=pcZrCond, out=pcCondOne);
      Or(a=pcCondOne, b=pcPosCond, out=pcJumpConds);
      // ...and C instruction
      And(a=instruction[15], b=pcJumpConds, out=pcJump);

      // increment if A instruction or no jump
      Not(in=pcJump, out=pcNoJump);
      Or(a=pcNoJump, b=isAInstruct, out=pcInc);

      PC(in=aReg, load=pcJump, inc=pcInc, reset=reset, out[0..14]=pc);
}
