// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx part
    And16(a=x, b=false, out=x0);
    Mux16(a=x, b=x0, sel=zx, out=xzx);
    //nx part
    Not16(in=xzx, out=xzxnx);
    Mux16(a=xzx, b=xzxnx, sel=nx, out=xOne);

    // zy part
    And16(a=y, b=false, out=y0);
    Mux16(a=y, b=y0, sel=zy, out=yzy);
    //ny part
    Not16(in=yzy, out=yzyny);
    Mux16(a=yzy, b=yzyny, sel=ny, out=yOne);

    // f part
    And16(a=xOne, b=yOne, out=xAndy);
    Add16(a=xOne, b=yOne, out=xPlusy);
    Mux16(a=xAndy, b=xPlusy, sel=f, out=outOne);

    // no part
    Not16(in=outOne, out=outNot);
    Mux16(a=outOne, b=outNot, sel=no, out=out, out[15]=outMsb, out[0..7]=outRightPart, out[8..15]=outLeftPart);

    // out zr
    Or8Way(in=outRightPart, out=outRightPartBit);
    Or8Way(in=outLeftPart, out=outLeftPartBit);
    Or(a=outRightPartBit, b=outLeftPartBit, out=zrNot);
    Not(in=zrNot, out=zr);

    // out ng
    And(a=outMsb, b=true, out=ng);
}
